# 中期

[TOC]

## 基础知识

核心

- 电机控制

- 传感器采集
- 云平台配置
- wifi 模组开发
- mqtt 协议

> 参考[太极创客](http://www.taichi-maker.com/)的教程来学习有关物联网的相关知识，为完成后续的毕设做准备。

## 部件

主要都是网购的部件，不过有一些零部件，买不到，就联系卖家，沟通好需求，通过注塑定制的。

- MQTT 通信协议的基本原理
- onenet 云平台的数据存储问题
- 梳理毕设作品的业务逻辑，完成代码。

![20211130171720](https://cdn.jsdelivr.net/gh/123taojiale/dahuyou_picture@main/blogs/20211130171720.png)

| 编号 | 描述                                                         |
| ---- | ------------------------------------------------------------ |
| 1    | [L298N 电机驱动模块](https://zhuanlan.zhihu.com/p/346930154) |
| 2    | [nodeMCU（esp 8266）](https://www.qutaojiao.com/course/nodemcu/base-nodemcu) |
| 3    | 光敏传感器                                                   |
| 4    | 雨滴模块                                                     |
| 5    | 电池                                                         |
| 6    | 电机                                                         |
| 其他 | 塑料 + 电线                                                  |



![20211130171214](https://cdn.jsdelivr.net/gh/123taojiale/dahuyou_picture@main/blogs/20211130171214.png)

**References**

- [Bilibili 产品是如何注塑成型的？想要全面了解模具，必须掌握原理](https://www.bilibili.com/s/video/BV1Yk4y1r7a7)
- [Bilibili 3分钟带你了解注塑工艺](https://www.bilibili.com/video/BV1RJ411G7ux?from=search&seid=8350638739067484516&spm_id_from=333.337.0.0)
- [太极创客 ESP8266-NodeMCU硬件参考](http://www.taichi-maker.com/homepage/reference-index/arduino-hardware-refrence/nodemcu/)
- [Bilibili nodemcu esp8266 l298n](https://search.bilibili.com/all?keyword=nodemcu%20esp8266%20l298n&from_source=webtop_search&spm_id_from=333.1007)
- [baidu nodemcu esp8266 l298n](https://www.baidu.com/s?wd=nodemcu%20esp8266%20l298n&rsv_spt=1&rsv_iqid=0x9018d97f00072c94&issp=1&f=8&rsv_bp=1&rsv_idx=2&ie=utf-8&tn=baiduhome_pg&rsv_enter=1&rsv_dl=tb&rsv_sug3=2&rsv_n=2&rsv_sug1=1&rsv_sug7=100&rsv_sug2=0&rsv_btype=i&inputT=350&rsv_sug4=434)


## 开发环境

Arduino IDE 下载：[download Arduino ide](https://www.arduino.cc/en/donate/)。

## codes

### 相关变量

```c++
#include <ESP8266WiFi.h> // 配置 wifi 模组网络通信的库
#include <PubSubClient.h> // mqtt 实现库
#include <ArduinoJson.h> // json 解析库
#include <Ticker.h> // 定时器库

#define WIFI_DEBUG 0 // 1：使用一键配网，其它值则使用默认无线账号密码
#define LightPIN A0
#define RainPIN 5
#define ENA 14 // D5
#define IN1 13 // D7
#define IN2 12 // D6
#define DEBUG 1
#define ONENET_DISCONNECTED 1   // 已经断开
#define ONENET_CONNECTED 2      // 已经连接上
#define ONENET_RECONNECT 3      // 重连成功
#define VER "ESP8266_MQTT_V1.0" // 版本号

const char *ssid = "aliyun";        // wifi账号
const char *password = "aliyun123"; // wifi密码

/*OneNet*/
PubSubClient mqttClient;
const char *mqttServer = "183.230.40.39";
const uint16_t mqttPort = 6002;
#define onenet_productId "455827"
#define onenet_deviceId "774347034"
#define onenet_apiKey "Khpiotxh03op0Fqd4tMclpCVyt8="
int state;
Ticker delayTimer;
WiFiClient espClient;

int rain; // 雨滴
int light; // 光照
int outlight;
boolean flag1 = true; // 标志位
boolean flag2 = true;
```

**注解**

- `const char *mqttServer = "183.230.40.39";` mqtt 服务器
- `const uint16_t mqttPort = 6002;` mqtt 端口号
- `#define onenet_productId "455827"` 产品 ID
- `#define onenet_deviceId "774347034"` 设备 ID
- `#define onenet_apiKey "Khpiotxh03op0Fqd4tMclpCVyt8="` API_KEY

### delayNs

```cpp
/* 延时N秒 */
void delayNs(uint8_t m)
{
  for (uint8_t index = 0; index < m; index++)
  {
    delay(1000);
    ESP.wdtFeed(); // 喂狗
  }
}
```

![6d6f1026f8e7045279867548577cf82](https://cdn.jsdelivr.net/gh/123taojiale/dahuyou_picture@main/blogs/6d6f1026f8e7045279867548577cf82.png)



- [x] 喂狗的目的是什么？

​	喂狗的目的，就是为了确系统能够正常运行。

**注解**

- 你在围着一座小山跑 `=>` 程序在按照预定流程执行
- 每次经过山脚下的某个地方，你都给大狼狗一根骨头 `=>` 喂狗
- 跑着跑着，你跑岔道了，跑到了不该去的山顶 `=>` 程序跑飞了
- 或者你在跑步的途中睡着了 `=>` 程序死机
- 这个时候这条大狼狗因为一直没有食物饿疯了，挣脱锁链来追你 `=>` 看门狗定时器溢出
- 你被它吓了一跳，从山上滚下来，起来一看又回到了出发地点，于是又从头开始跑 `=>` 看门狗重置

有一只狗一直盯着系统的定时器，如果定时器的时间值到了，你还没喂它东西的话，它就会把你的系统重启，因为它认为这么久不喂它吃东西程序肯定跑飞了（其实有可能是有比较长的延时）。所以，为了保持系统的正常运行，就要在定时器到之前不断的给它喂东西吃。

### delayRestart

```cpp
/* 延时重启 */
void delayRestart(float t)
{
  Serial.print("Restart after ");
  Serial.print(t);
  Serial.println("s");
  delayTimer.attach(t, []()
                    {
                      Serial.println("\r\nRestart now!");
                      ESP.restart();
                    });
}
```

**注解**

- Serial 表示串行

### autoConfig

```cpp
/* 自动连接 */
bool autoConfig()
{
  WiFi.begin();
  for (int i = 0; i < 20; i++)
  {
    if (WiFi.status() == WL_CONNECTED)
    {
      Serial.println("AutoConfig Success");
      Serial.printf("SSID:%s\r\n", WiFi.SSID().c_str());
      Serial.printf("PSW:%s\r\n", WiFi.psk().c_str());
      WiFi.printDiag(Serial);
      return true;
    }
    else
    {
      Serial.print("AutoConfig Waiting......");
      Serial.println(WiFi.status());
      delay(1000);
    }
  }
  Serial.println("AutoConfig Faild!");
  return false;
}
```

### smartConfig

```cpp
/* 一键配网 */
void smartConfig()
{
  WiFi.mode(WIFI_STA);
  Serial.println("\r\nWait for Smartconfig");
  WiFi.beginSmartConfig();
  while (1)
  {
    Serial.print(".");
    digitalWrite(LED_BUILTIN, !digitalRead(LED_BUILTIN));
    if (WiFi.smartConfigDone())
    {
      Serial.println("SmartConfig Success");
      Serial.printf("SSID:%s\r\n", WiFi.SSID().c_str());
      Serial.printf("PSW:%s\r\n", WiFi.psk().c_str());
      WiFi.setAutoConnect(true); // 设置自动连接
      break;
    }
    delay(1000);
  }
}
```

**注解**

- `WiFi.mode(WIFI_STA);` 设置 ESP8266 工作模式为无线终端模式。

### connectToOneNetMqtt

```cpp
/* 连接OneNet */
int connectToOneNetMqtt()
{
  int cnt = 0;
  while (!mqttClient.connected())
  {
    ESP.wdtFeed();
    cnt++;
    Serial.println("Connect to OneNet MQTT...");

    if (mqttClient.connect(onenet_deviceId, onenet_productId, onenet_apiKey))
    {
      Serial.println("connect success!");
      return ONENET_RECONNECT;
    }
    else
    {
      Serial.print("connect fail!");
      Serial.println(" try again in 5 seconds");
      delayNs(5);
    }
    if (cnt >= 10)
    {
      //只做10次连接到OneNet，连接不上重启8266
      cnt = 0;
      delayRestart(1);
    }
  }
  return ONENET_CONNECTED;
}
```

### callback

```cpp
/* 云端下发 */
void callback(char *topic, byte *payload, unsigned int length)
{
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  for (int i = 0; i < length; i++)
  {
    Serial.print((char)payload[i]);
  }
  Serial.println();
  if ((char)payload[0] == '0')
  {
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, HIGH);
    delay(3000);
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, LOW);
  }
  else if ((char)payload[0] == '1')
  {
    digitalWrite(IN1, HIGH);
    digitalWrite(IN2, LOW);
    delay(3000);
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, LOW);
  }
}
```

### Rain_pubMQTTmsg

```cpp
/* 发布雨滴信息 */
void Rain_pubMQTTmsg(uint32_t data)
{
  long lastMsg = 0;

  char msg[50];
  char tmp[28];
  char d[3];
  snprintf(tmp, sizeof(tmp), "{\"rain\":%d}", data);
  uint16_t streamLen = strlen(tmp);

  d[0] = '\x03';
  d[1] = (streamLen >> 8);
  d[2] = (streamLen & 0xFF);
  snprintf(msg, sizeof(msg), "%c%c%c%s", d[0], d[1], d[2], tmp);
  mqttClient.publish("$dp", (uint8_t *)msg, streamLen + 3, false);
}
```

### ILL_pubMQTTmsg

```cpp
/*发布光照信息*/
void ILL_pubMQTTmsg(uint32_t data)
{
  long lastMsg = 0;

  char msg[50];
  char tmp[28];
  char d[3];
  snprintf(tmp, sizeof(tmp), "{\"light\":%d}", data);
  uint16_t streamLen = strlen(tmp);

  d[0] = '\x03';
  d[1] = (streamLen >> 8);
  d[2] = (streamLen & 0xFF);
  snprintf(msg, sizeof(msg), "%c%c%c%s", d[0], d[1], d[2], tmp);
  mqttClient.publish("$dp", (uint8_t *)msg, streamLen + 3, false);
}
```

### initSystem

```cpp
/* 初始化系统 */
void initSystem()
{
  int cnt = 0;
  Serial.begin(115200);
  Serial.println("\r\n\r\nStart ESP8266 MQTT");
  Serial.print("Firmware Version:");
  Serial.println(VER);
  Serial.print("SDK Version:");
  pinMode(LED_BUILTIN, OUTPUT);
  digitalWrite(LED_BUILTIN, HIGH);
  Serial.println(ESP.getSdkVersion());

  ESP.wdtEnable(5000);

  if (WIFI_DEBUG == 1) //开启一键配网模式
  {
    if (!autoConfig())
    {
      Serial.println("Start smartConfig");
      smartConfig();
    }
  }
  else
  {
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED)
    {
      delay(500);
      cnt++;
      Serial.print(".");
      if (cnt >= 40)
      {
        cnt = 0;
        //重启系统
        delayRestart(1);
      }
    }
  }
  Serial.print("WIFI Connect \r\n");
}
```

**注解**

- `Serial.begin(115200);` 开启 nodeMCU 与电脑的串口通讯，115200 是「波特率」，115200 bit/s，表示 nodeMCU 与电脑之间，每秒能传输 11 万 5 千 2 百个位（约 14KB）的资料。

### initOneNetMqtt

```cpp
/* 初始化ONENET通信 */
void initOneNetMqtt()
{
  mqttClient.setServer(mqttServer, mqttPort);
  mqttClient.setClient(espClient);
  mqttClient.setCallback(callback);
}
```

**注解**

- `mqttClient.setServer(mqttServer, mqttPort);` 设置 mqtt 服务器和端口号
- 

### setup

```cpp
/* 初始化 */
void setup()
{
  initSystem();
  initOneNetMqtt();
  pinMode(LightPIN, INPUT);
  pinMode(RainPIN, INPUT);
  pinMode(IN1, OUTPUT);
  pinMode(IN2, OUTPUT);
  pinMode(ENA, OUTPUT);
  digitalWrite(ENA, HIGH);
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, LOW);
  /* initialize serial for debugging */
  Serial.begin(115200);
}
```

### loop

```cpp
/* 主函数 */
void loop()
{
  ESP.wdtFeed();
  rain = !digitalRead(RainPIN);
  light = analogRead(LightPIN);
  outlight = map(light, 1023, 0, 0, 100);
  state = connectToOneNetMqtt();
  Serial.println(WiFi.status());
  if (state == ONENET_RECONNECT)
  {
    mqttClient.loop();
  }
  else if (state == ONENET_CONNECTED)
  {
    Rain_pubMQTTmsg(rain);
    ILL_pubMQTTmsg(outlight);
    mqttClient.loop();
  }
  if ((rain == 1 || outlight <= 40) && flag1)
  {
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, HIGH);
    delay(3000);
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, LOW);
    flag1 = false;
    flag2 = true;
  }
  else if (rain == 0 && outlight > 40 && flag2)
  {
    digitalWrite(IN1, HIGH);
    digitalWrite(IN2, LOW);
    delay(3000);
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, LOW);
    flag2 = false;
    flag1 = true;
  }

  delay(2000);
}
```

### 注解

**识别设备的标识**

- access_key：ym9Y7hkHbmgCIPUT3MUYStgc3QuRk7Z/HAkUXpMhZYs=
- 产品ID：455827
- 设备ID：774347034

**通信协议**

MQTT

## 中移 Onenet 云平台

![20211127143220](https://cdn.jsdelivr.net/gh/123taojiale/dahuyou_picture@main/blogs/20211127143220.png)

![20211127143234](https://cdn.jsdelivr.net/gh/123taojiale/dahuyou_picture@main/blogs/20211127143234.png)

